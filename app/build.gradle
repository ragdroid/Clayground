plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion AndroidSdk.compileSdk

    defaultConfig {
        applicationId "com.ragdroid.clayground"
        minSdkVersion AndroidSdk.minSdk
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }


    buildTypes {
        debug {
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        compose true

        // Disable unused AGP features
        buildConfig false
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        // Multiple dependency bring these files in. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation AndroidX.coreKtx
    implementation AndroidX.appcompat
    implementation Google.material
    implementation Compose.activity
    implementation Compose.ui
    implementation Compose.material
    implementation Compose.materialIcons
    implementation Compose.uiTooling
    implementation AndroidX.lifecycleRuntimeKtx
    implementation(Coroutines.coreNative) {
        version {
            strictly '1.4.3-native-mt'
        }
    }
    implementation project(':shared')
    implementation project(':base-mvi')

    implementation DI.hilt
    implementation DI.lifecycle
    kapt DI.kaptHilt
    kapt DI.kaptLifecycle
    implementation Other.timber

    testImplementation Testing.junit

    androidTestImplementation Testing.androidXTestExt
    androidTestImplementation Compose.composeUiTest
}
